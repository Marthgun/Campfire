### Form/CampUtil

**SCRIPTNAME.**

    scriptname FrostUtil hidden

**OVERVIEW.**

The \`FrostUtil\` script is the primary way most mods should interact with Frostfall and contains many helpful functions. To call any of the following functions, download the SDK and in your script include the line: \<pre\>import FrostUtil\</pre\> Alternatively, you can call \`FrostUtil.FunctionName()\` without importing FrostUtil. }

### FrostUtil/GetAPIVersion

**DESCRIPTION.**

Get the FrostUtil API version number.

**API VERSION ADDED.**

1

**SYNTAX.**

    float function GetAPIVersion() global

**PARAMETERS.**

None

**RETURN VALUE.**

The FrostUtil API version number. This is NOT the same thing as the version number of Frostfall. FrostUtil's API version number will increment only when changes have been made to the API itself.

**EXAMPLES.**

    float ver = FrostUtil.GetAPIVersion()

### FrostUtil/GetFrostfallVersion

**DESCRIPTION.**

Get the Frostfall mod version number.

**API VERSION ADDED.**

1

**SYNTAX.**

    float function GetFrostfallVersion() global

**PARAMETERS.**

None

**RETURN VALUE.**

The Frostfall version number.

**EXAMPLES.**

    float ver = FrostUtil.GetFrostfallVersion()

### FrostUtil/IsPlayerNearFire

**DESCRIPTION.**

Whether or not the player is currently near a fire.

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsPlayerNearFire() global

**PARAMETERS.**

None

**RETURN VALUE.**

True if near a fire; false otherwise.

**EXAMPLES.**

    ;Is the player near a fire?
    bool near_fire = FrostUtil.IsPlayerNearFire()

**NOTES.**

This does NOT indicate if the player is near a heat source; there are heat sources that are not fires. Fires have special properties in Frostfall and Campfire, which include drying the player when wet, and being able to cook food using a cooking pot.

To determine if the player is near a heat source of any kind, use [GetPlayerHeatSourceLevel](#robo4)() instead.

Keep in mind that Frostfall's Heat Source System only updates every 5 seconds. Therefore, the data returned by this function is not real-time.

### FrostUtil/GetPlayerHeatSourceLevel

**DESCRIPTION.**

The level (size) of the player's current nearby heat source.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerHeatSourceLevel() global

**PARAMETERS.**

None

**RETURN VALUE.**

Possible return values:

-   0 = Player is not near a heat source.

-   1 = Current heat source is "small" (e.g. bowls of burning embers, "fragile" campfires in Campfire)

-   2 = Current heat source is "medium" (e.g. most campfires and other fireplaces)

-   3 = Current heat source is "large" (e.g. giant campfires, "roaring" campfires in Campfire)

**EXAMPLES.**

    ;Is the player near heat?
    int heat = FrostUtil.GetPlayerHeatSourceLevel()
    if heat > 0
        debug.notification("Player is near a heat source!")
    endif

**NOTES.**

This function does NOT determine if the heat source is a fire or not; there are heat sources that are not fires. To determine if the player is near a fire, use [IsPlayerNearFire](#robo3)().

Keep in mind that Frostfall's Heat Source System only updates every 5 seconds. Therefore, the data returned by this function is not real-time.

### FrostUtil/GetPlayerHeatSourceDistance

**DESCRIPTION.**

The distance from the player to the player's current nearby heat source.

**API VERSION ADDED.**

1

**SYNTAX.**

    float function GetPlayerHeatSourceDistance() global

**PARAMETERS.**

None

**RETURN VALUE.**

-   0.0 to 600.0 - The distance from the player to the heat source. Frostfall does not detect heat sources more than 600 units away.

-   -1.0 - No current heat source detected.

**EXAMPLES.**

    ;How far away is the heat source?
    float dist = FrostUtil.GetPlayerHeatSourceDistance()
    if dist <= 300.0
        debug.notification("The player is really close to the heat source!")
    endif

**NOTES.**

Keep in mind that Frostfall's Heat Source System only updates every 5 seconds. Therefore, the data returned by this function is not real-time.

### FrostUtil/IsPlayerTakingShelter

**DESCRIPTION.**

Whether or not the player is taking shelter underneath an outcropping / building. If the player is "sheltered", the player will dry off if wet, regardless of current weather conditions.

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsPlayerTakingShelter() global

**PARAMETERS.**

None

**RETURN VALUE.**

True if taking shelter, false if not.

**EXAMPLES.**

    ;Is the player underneath something?
    bool sheltered = FrostUtil.IsPlayerTakingShelter()

**NOTES.**

This function does not return true if the player is inside a Campfire-based tent; it only returns true if a ray emitting upward from the player's head collides with an obstruction. For tent detection, use [CampUtil](#robo0).[GetCurrentTent](#robo69)().

### FrostUtil/IsNearFastTravelException

**DESCRIPTION.**

Whether or not the player near a "fast travel exception". If a player is nearby one of these objects, the Exposure System will re-enable the player's Fast Travel controls. This is used in cases where there is an expectation for Fast Travel be enabled when near these objects (like Apocrypha's Black Books.)

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsNearFastTravelException() global

**PARAMETERS.**

None

**RETURN VALUE.**

True if near a "fast travel exception" object, false if not.

**EXAMPLES.**

    ;Is the player near a fast travel exception?
    bool near_ft_exception = FrostUtil.IsNearFastTravelException()

### FrostUtil/GetCurrentTemperature

**DESCRIPTION.**

Returns the current area temperature, based on location, weather, weather severity, and time of day.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetCurrentTemperature() global

**PARAMETERS.**

None

**RETURN VALUE.**

-   Greater than 10: A "warm" area. Exposure will decrease. The higher the number, the faster it will decrease.

-   10: A "neutral" area. Exposure will not increase or decrease.

-   Less than 10: A "cold" area. Exposure will increase. The lower the number, the faster it will increase.

**EXAMPLES.**

    int temp = FrostUtil.GetCurrentTemperature()
    if temp < 10
        debug.notification("It's cold!")
    endif

**NOTES.**

The return values of this function correspond very roughly with degrees celsius.

### FrostUtil/GetCurrentWeatherActual

**DESCRIPTION.**

Returns the current weather somewhat more accurately than the default GetCurrentWeather() function. This function returns the outgoing weather if the weather is currently transitioning out (and thus is still currently visible). Otherwise, returns the current weather.

**API VERSION ADDED.**

1

**SYNTAX.**

    Weather function GetCurrentWeatherActual() global

**PARAMETERS.**

None

**RETURN VALUE.**

The current weather.

**EXAMPLES.**

    ;What is the current weather, really?
    Weather wthr = FrostUtil.GetCurrentWeatherActual()

### FrostUtil/GetWeatherClassificationActual

**DESCRIPTION.**

Returns the classification of the weather returned by [GetCurrentWeatherActual](#robo9)().

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetWeatherClassificationActual(Weather akWeather) global

**PARAMETERS.**

-   akWeather: The Weather to check.

**RETURN VALUE.**

The current weather classification.

**EXAMPLES.**

    ;What is the current weather classification, really?
    int class = FrostUtil.GetWeatherClassificationActual()

### FrostUtil/IsWeatherSevere

**DESCRIPTION.**

True if this weather is in the severe weather list that Frostfall maintains, false if not.

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsWeatherSevere(Weather akWeather) global

**PARAMETERS.**

-   akWeather: The Weather to check.

**RETURN VALUE.**

Whether or not this weather is severe.

**EXAMPLES.**

    if FrostUtil.IsWeatherSevere(FrostUtil.GetCurrentWeatherActual())
        debug.notification("Better stay indoors!")
    endif

### FrostUtil/AddSevereWeather

**DESCRIPTION.**

Adds a weather to the severe weather list that Frostfall maintains.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddSevereWeather(Weather akWeather) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Add my custom blizzard to the severe weather list
    FrostUtil.AddSevereWeather(my_crazy_blizzard)

### FrostUtil/RemoveSevereWeather

**DESCRIPTION.**

Removes a weather from the severe weather list that Frostfall maintains. Only weather forms added to the list via [AddSevereWeather](#robo12) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveSevereWeather(Weather akWeather) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Remove my custom blizzard to the severe weather list
    FrostUtil.RemoveSevereWeather(my_crazy_blizzard)

### FrostUtil/IsWeatherOvercast

**DESCRIPTION.**

True if this weather is in the overcast weather list that Frostfall maintains, false if not.

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsWeatherOvercast(Weather akWeather) global

**PARAMETERS.**

-   akWeather: The Weather to check.

**RETURN VALUE.**

Whether or not this weather is Overcast.

**EXAMPLES.**

    if FrostUtil.IsWeatherOvercast(FrostUtil.GetCurrentWeatherActual())
        debug.notification("Man, it's dreary out.")
    endif

**NOTES.**

Weather in the overcast weather list must be marked with the "Cloudy" classification. This list is necessary because the base Skyrim game often uses the "Cloudy" classification to denote fog weathers instead of cloudy weathers, and Frostfall needs to be able to differentiate between them.

### FrostUtil/AddOvercastWeather

**DESCRIPTION.**

Adds a weather to the overcast weather list that Frostfall maintains.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddOvercastWeather(Weather akWeather) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Add my custom weather to the overcast weather list
    FrostUtil.AddOvercastWeather(my_dreary_weather)

**NOTES.**

Weather in the overcast weather list must be marked with the "Cloudy" classification. This list is necessary because the base Skyrim game often uses the "Cloudy" classification to denote fog weathers instead of cloudy weathers, and Frostfall needs to be able to differentiate between them.

### FrostUtil/RemoveOvercastWeather

**DESCRIPTION.**

Removes a weather from the overcast weather list that Frostfall maintains. Only weather forms added to the list via [AddOvercastWeather](#robo15) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveOvercastWeather(Weather akWeather) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Remove my custom weather from the overcast weather list
    FrostUtil.RemoveOvercastWeather(my_dreary_weather)

**NOTES.**

Weather in the overcast weather list must be marked with the "Cloudy" classification. This list is necessary because the base Skyrim game often uses the "Cloudy" classification to denote fog weathers instead of cloudy weathers, and Frostfall needs to be able to differentiate between them.

### FrostUtil/IsRefInOblivion

**DESCRIPTION.**

Whether or not the reference is in a base game or DLC Oblivion worldspace.

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsRefInOblivion(ObjectReference akReference) global

**PARAMETERS.**

-   akReference: The object reference to check.

**RETURN VALUE.**

True if the reference is in Oblivion.

**EXAMPLES.**

    ;Is the box in Oblivion?
    if IsRefInOblivion(Box)
        Debug.Trace("Box is in Oblivion!")
    endif

**NOTES.**

-   The following Worldspaces are considered Oblivion worldspaces:

-   Soul Cairn

-   Apocrypha

### FrostUtil/IsOblivionWorldspace

**DESCRIPTION.**

Whether or not the worldspace is a base game or DLC Oblivion worldspace.

**API VERSION ADDED.**

1

**SYNTAX.**

    bool function IsOblivionWorldspace(Worldspace akWorldspace) global

**PARAMETERS.**

-   akWorldspace: The worldspace to check.

**RETURN VALUE.**

True if the worldspace is a plane of Oblivion.

**EXAMPLES.**

    ;Is the box in Oblivion?
    if IsOblivionWorldspace(infernal_realm)
        Debug.Trace("This is a plane of Oblivion!")
    endif

**NOTES.**

-   The following Worldspaces are considered Oblivion worldspaces by default:

-   Soul Cairn

-   Apocrypha

### FrostUtil/AddOblivionWorldspace

**DESCRIPTION.**

Adds a worldspace to the Oblivion worldspace list that Frostfall maintains. The player does not gain exposure when in planes of Oblivion.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddOblivionWorldspace(Worldspace akWorldspace) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Add my custom worldspace to the Oblivion worldspace list
    FrostUtil.AddOblivionWorldspace(my_infernal_realm)

### FrostUtil/RemoveOblivionWorldspace

**DESCRIPTION.**

Removes a worldspace from the Oblivion worldspace list that Frostfall maintains. The player does not gain exposure when in planes of Oblivion. Only Worldspace forms added to the list via [AddOblivionWorldspace](#robo19) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveOblivionWorldspace(Worldspace akWorldspace) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Remove my custom worldspace from the Oblivion worldspace list
    FrostUtil.RemoveOblivionWorldspace(my_infernal_realm)

### FrostUtil/IsExposureException

**DESCRIPTION.**

True if this form is in the exposure exception list that Frostfall maintains, false if not. When the player goes near this object (600 units or less), the player will not gain or lose exposure.

**API VERSION ADDED.**

2

**SYNTAX.**

    bool function IsExposureException(Form akBaseObject) global

**PARAMETERS.**

-   akBaseObject: The form to check.

**RETURN VALUE.**

Whether or not this form is an exposure exception.

**EXAMPLES.**

    ;Will I turn off exposure mechanics near the dragon?
    if FrostUtil.IsExposureException(SuperDragon)
        debug.notification("Guess I won't freeze to death near the Super Dragon.")
    endif

### FrostUtil/AddExposureException

**DESCRIPTION.**

Adds a form to the list of exposure exception objects that Frostfall maintains. When the player goes near this object (600 units or less), the player will not gain or lose exposure.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddExposureException(Form akBaseObject) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Suspend exposure gain and loss near the power stone
    FrostUtil.AddExposureException(power_stone)

### FrostUtil/RemoveExposureException

**DESCRIPTION.**

Removes a form from the list of exposure exception objects that Frostfall maintains. Only forms added to the list via [AddExposureException](#robo22) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveExposureException(Form akBaseObject) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;No longer suspend exposure near the power stone
    FrostUtil.RemoveExposureException(power_stone)

### FrostUtil/IsFastTravelException

**DESCRIPTION.**

True if this form is in the fast travel exception list that Frostfall maintains, false if not. When the player goes near this object (600 units or less), fast travel controls will be re-enabled regardless of their fast travel settings.

**API VERSION ADDED.**

2

**SYNTAX.**

    bool function IsFastTravelException(Form akBaseObject) global

**PARAMETERS.**

-   akBaseObject: The form to check.

**RETURN VALUE.**

Whether or not this form is a fast travel exception.

**EXAMPLES.**

    ;Will I enable fast travel near the carriage?
    if FrostUtil.IsFastTravelException(carriage)
        debug.notification("Sally forth!")
    endif

### FrostUtil/AddFastTravelException

**DESCRIPTION.**

Adds a form to the list of fast travel exception objects that Frostfall maintains. When the player goes near this object (600 units or less), fast travel controls will be re-enabled regardless of their fast travel settings.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddFastTravelException(Form akBaseObject) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Enable fast travel when near the horse.
    FrostUtil.AddFastTravelException(my_horse)

### FrostUtil/RemoveFastTravelException

**DESCRIPTION.**

Removes a form from the list of fast travel exception objects that Frostfall maintains. Only forms added to the list via [AddFastTravelException](#robo25) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveFastTravelException(Form akBaseObject) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;No longer enable fast travel near the horse
    FrostUtil.RemoveFastTravelException(my_horse)

### FrostUtil/IsSleepException

**DESCRIPTION.**

True if this form is in the sleep exception list that Frostfall maintains, false if not.

**API VERSION ADDED.**

2

**SYNTAX.**

    bool function IsSleepException(Form akBaseObject) global

**PARAMETERS.**

-   akBaseObject: The form to check.

**RETURN VALUE.**

Whether or not this weather is Overcast.

**EXAMPLES.**

    if FrostUtil.IsSleepException(my_bedroll)
        debug.notification("Time to hit the sack!")
    endif

**NOTES.**

When the player goes near this object (600 units or less), the player will be able to sleep. This is important if the player has "Disable Waiting Outdoors" enabled, which prevents sleeping unless near an object in this list.

Tents created with the Campfire Dev Kit are automatically sleep exception objects. Conjured shelters are not and must be added using [AddSleepException](#robo28)().

### FrostUtil/AddSleepException

**DESCRIPTION.**

Adds a form to the list of sleep exception objects that Frostfall maintains. When the player goes near this object (600 units or less), the player will be able to sleep. This is important if the player has "Disable Waiting Outdoors" enabled, which prevents sleeping unless near an object in this list.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddSleepException(Form akBaseObject) global

**PARAMETERS.**

-   akBaseObject: The form to add.

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Enable ability to sleep near my bedroll.
    FrostUtil.AddSleepException(my_bedroll)

### FrostUtil/RemoveSleepException

**DESCRIPTION.**

Removes a form from the list of sleep exception objects that Frostfall maintains. Only forms added to the list via [AddSleepException](#robo28) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveSleepException(Form akBaseObject) global

**PARAMETERS.**

-   akBaseObject: The form to remove.

**RETURN VALUE.**

None

**EXAMPLES.**

    ;No longer enable fast travel near the horse
    FrostUtil.RemoveSleepException(my_horse)

### FrostUtil/AddFastTravelWorldspaceException

**DESCRIPTION.**

Adds a worldspace to the list of worldspaces that Frostfall maintains where fast travel controls should always be enabled.

**API VERSION ADDED.**

2

**SYNTAX.**

    function AddFastTravelWorldspaceException(Worldspace akWorldspace) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Add my custom worldspace to the fast travel exception worldspace list
    FrostUtil.AddFastTravelWorldspaceException(my_realm)

### FrostUtil/RemoveFastTravelWorldspaceException

**DESCRIPTION.**

Adds a worldspace to the list of worldspaces that Frostfall maintains where fast travel controls should always be enabled. Only Worldspace forms added to the list via [AddFastTravelWorldspaceException](#robo30) can be removed.

**API VERSION ADDED.**

2

**SYNTAX.**

    function RemoveFastTravelWorldspaceException(Worldspace akWorldspace) global

**PARAMETERS.**

None

**RETURN VALUE.**

None

**EXAMPLES.**

    ;Remove my custom worldspace from the fast travel exception worldspace list
    FrostUtil.RemoveFastTravelWorldspaceException(my_realm)

### FrostUtil/GetPlayerWetness

**DESCRIPTION.**

Return the player's current wetness value.

**API VERSION ADDED.**

1

**SYNTAX.**

    float function GetPlayerWetness() global

**PARAMETERS.**

None

**RETURN VALUE.**

-   The player's current wetness value.

-   0.0 = Not wet

-   \>0.0 - 199.9 = Damp

-   200.0 - 549.9 = Wet

-   550.0 - 750.0 = Drenched

**EXAMPLES.**

    float wetness = FrostUtil.GetPlayerWetness()

### FrostUtil/GetPlayerWetnessLevel

**DESCRIPTION.**

Return the player's current wetness level. May be easier to use than [GetPlayerWetness](#robo32)() if you don't need to know the actual wetness value.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerWetnessLevel() global

**PARAMETERS.**

None

**RETURN VALUE.**

-   The player's current wetness level.

-   0 = Not wet

-   1 = Damp

-   2 = Wet

-   3 = Drenched

**EXAMPLES.**

    ;Is the player drenched?
    int wet_level = FrostUtil.GetPlayerWetnessLevel()
    if wet_level == 3
        debug.notification("You're soaked!")
    endif

### FrostUtil/ModPlayerWetness

**DESCRIPTION.**

Modify the player's current wetness by the given amount. Using this function will correctly process the wetness change and notify the UI layer (meters, etc) of the change and display them as appropriate. Using this function is the only safe method of directly modifying the player's wetness value.

**API VERSION ADDED.**

1

**SYNTAX.**

    function ModPlayerWetness(float amount, float limit = -1.0) global

**PARAMETERS.**

-   amount: The amount to modify the player's wetness by. Positive numbers increase wetness, negative values decrease wetness.

-   limit (optional): The value at which you should stop the wetness change, if this value is reached. A value of -1.0 indicates that there is no limit. (Example: calling FrostUtil.ModPlayerWetness(35.0, 50.0) when the player's current wetness is 40.0 will increase the player's wetness to 50.0, not 75.0)

**RETURN VALUE.**

None

**EXAMPLES.**

    ;The player did something that should make them dry off.
    FrostUtil.ModPlayerWetness(-15.0)
    ;The player did something that should make them dry off, but not below "Wet".
    FrostUtil.ModPlayerWetness(-15.0, 200.0)
    ;The player did something that should make them wetter.
    FrostUtil.ModPlayerWetness(20.0)
    ;The player did something that should make them wetter, but not above "Wet".
    FrostUtil.ModPlayerWetness(20.0, 200.0)

### FrostUtil/GetPlayerExposure

**DESCRIPTION.**

Return the player's current exposure value.

**API VERSION ADDED.**

1

**SYNTAX.**

    float function GetPlayerExposure() global

**PARAMETERS.**

None

**RETURN VALUE.**

-   The player's current exposure.

-   0.0 - 19.9 = Warm

-   20.0 - 39.9 = Comfortable

-   40.0 - 59.9 = Cold

-   60.0 - 79.9 = Very Cold

-   80.0 - 99.9 = Freezing

-   100.0 - 120.0 = Freezing to Death

**EXAMPLES.**

    ;Is the player cold?
    float exp = FrostUtil.GetPlayerExposure()
    if exp >= 40.0
        debug.notification("Brrr!")
    endif

### FrostUtil/GetPlayerExposureLevel

**DESCRIPTION.**

Return the player's current exposure level. May be easier to use than [GetPlayerExposure](#robo35)() if you don't need to know the actual exposure value.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerExposureLevel() global

**PARAMETERS.**

None

**RETURN VALUE.**

-   The player's current exposure level.

-   -1 = Completely Warm

-   0 = Warm

-   1 = Comfortable

-   2 = Cold

-   3 = Very Cold

-   4 = Freezing

-   5 = Freezing to Death

-   6 = Maximum Exposure

**EXAMPLES.**

    ;Is the player cold?
    float exp_level = FrostUtil.GetPlayerExposureLevel()
    if exp_level >= 2
        debug.notification("Brrr!")
    endif

### FrostUtil/ModPlayerExposure

**DESCRIPTION.**

Modify the player's current exposure by the given amount. Using this function will correctly process the exposure change and notify the UI layer (meters, etc) of the change and display them as appropriate. Using this function is the only safe method of directly modifying the player's exposure value.

**API VERSION ADDED.**

1

**SYNTAX.**

    function ModPlayerExposure(float amount, float limit = -1.0) global

**PARAMETERS.**

-   amount: The amount to modify the player's exposure by. Positive numbers increase exposure, negative values decrease exposure.

-   limit (optional): The value at which you should stop the exposure change, if this value is reached. A value of -1.0 indicates that there is no limit. (Example: calling FrostUtil.ModPlayerExposure(35.0, 50.0) when the player's current exposure is 40.0 will increase the player's exposure to 50.0, not 75.0)

**RETURN VALUE.**

None

**EXAMPLES.**

    ;The player did something that should make them warmer.
    FrostUtil.ModPlayerExposure(-15.0)
    ;The player did something that should make them warmer, but not below "Cold".
    FrostUtil.ModPlayerExposure(-15.0, 40.0)
    ;The player did something that should make them colder.
    FrostUtil.ModPlayerExposure(20.0)
    ;The player did something that should make them colder, but not above "Freezing".
    FrostUtil.ModPlayerExposure(20.0, 80.0)

### FrostUtil/GetPlayerWarmth

**DESCRIPTION.**

Returns the player's total Warmth value.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerWarmth() global

**PARAMETERS.**

None

**RETURN VALUE.**

The player's Warmth value.

**EXAMPLES.**

    ;How bundled up is the player?
    int warmth = FrostUtil.GetPlayerWarmth()

### FrostUtil/GetPlayerCoverage

**DESCRIPTION.**

Returns the player's total Coverage value.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerCoverage() global

**PARAMETERS.**

None

**RETURN VALUE.**

The player's Coverage value.

**EXAMPLES.**

    ;How covered is the player?
    int coverage = FrostUtil.GetPlayerCoverage()

### FrostUtil/GetPlayerArmorWarmth

**DESCRIPTION.**

Returns the sum of the Warmth values of all gear worn by the player.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerArmorWarmth() global

**PARAMETERS.**

None

**RETURN VALUE.**

The total Warmth of all player worn gear.

**EXAMPLES.**

    ;How warm are the player's clothes?
    int gear_warmth = FrostUtil.GetPlayerArmorWarmth()

**NOTES.**

There are many different sources of Warmth, so this function does not return the player's total Warmth value. If you want the total Warmth, use [GetPlayerWarmth](#robo38)() instead.

### FrostUtil/GetPlayerArmorCoverage

**DESCRIPTION.**

Returns the sum of the Coverage values of all gear worn by the player.

**API VERSION ADDED.**

1

**SYNTAX.**

    int function GetPlayerArmorCoverage() global

**PARAMETERS.**

None

**RETURN VALUE.**

The total Coverage of all player worn gear.

**EXAMPLES.**

    ;How warm are the player's clothes?
    int gear_coverage = FrostUtil.GetPlayerArmorCoverage()

**NOTES.**

There are many different sources of Coverage, so this function does not return the player's total Coverage value. If you want the total Coverage, use [GetPlayerCoverage](#robo39)() instead.

### FrostUtil/Frostfall\_OnPlayerStartSwimming

**DESCRIPTION.**

An SKSE Mod Event that is raised when the player begins swimming.

**API VERSION ADDED.**

1

**SYNTAX.**

    Event Frostfall_OnPlayerStartSwimming()

**PARAMETERS.**

None

**EXAMPLES.**

    Event OnInit()
        RegisterForModEvent("Frostfall_OnPlayerStartSwimming", "Frostfall_OnPlayerStartSwimming")
    endEvent

    Event Frostfall_OnPlayerStartSwimming()
        debug.notification("The player started swimming!")
    endEvent

### FrostUtil/Frostfall\_OnPlayerStopSwimming

**DESCRIPTION.**

An SKSE Mod Event that is raised when the player stops swimming.

**API VERSION ADDED.**

1

**SYNTAX.**

    Event Frostfall_OnPlayerStopSwimming()

**PARAMETERS.**

None

**EXAMPLES.**

    Event OnInit()
        RegisterForModEvent("Frostfall_OnPlayerStopSwimming", "Frostfall_OnPlayerStopSwimming")
    endEvent

    Event Frostfall_OnPlayerStopSwimming()
        debug.notification("The player stopped swimming!")
    endEvent

### FrostUtil/Frostfall\_Loaded

**DESCRIPTION.**

An SKSE Mod Event that is raised when Frostfall is finished starting up, or after loading a save game with Frostfall enabled.

**API VERSION ADDED.**

1

**SYNTAX.**

    Event Frostfall_Loaded()

**PARAMETERS.**

None

**EXAMPLES.**

    Event OnInit()
        RegisterForModEvent("Frostfall_Loaded", "Frostfall_Loaded")
    endEvent

    Event Frostfall_Loaded()
        debug.notification("Frostfall has finished starting up!")
    endEvent

**NOTES.**

This event will only be raised if Frostfall is enabled. Therefore, don't rely on this event at game start-up for a critical function as you will not receive it until the player starts up Frostfall or loads a game with Frostfall already enabled.

### FrostUtil/Frost\_OnRescuePlayer

**DESCRIPTION.**

An SKSE Mod Event that is raised when the player is rescued when using the Max Exposure Rescue mechanic.

**API VERSION ADDED.**

1

**SYNTAX.**

    Event Frost_OnRescuePlayer(bool in_water)

**PARAMETERS.**

-   in\_water: Whether or not the player was swimming when they were rescued (mainly used by the rescue system to select a suitable destination).

**EXAMPLES.**

    Event OnInit()
        RegisterForModEvent("Frost_OnRescuePlayer", "Frost_OnRescuePlayer")
    endEvent

    Event Frost_OnRescuePlayer(bool in_water)
        debug.notification("The player was just rescued!")
    endEvent

### FrostUtil/Frost\_OnTamrielRegionChange

**DESCRIPTION.**

An SKSE Mod Event that is raised when the player moves from one tracked region to another.

**API VERSION ADDED.**

1

**SYNTAX.**

    Event OnTamrielRegionChange(int region, bool in_region)

**PARAMETERS.**

-   region: The ID of the region. See Notes for region IDs.

-   in\_region: If True, the player is entering this region. If False, the player is leaving it.

**EXAMPLES.**

    Event OnInit()
        RegisterForModEvent("Frost_OnTamrielRegionChange", "Frost_OnTamrielRegionChange")
    endEvent

    Event Frost_OnTamrielRegionChange(int region, bool in_region)
        if in_region && region == 11
            debug.notification("The player just entered Solstheim!")
        endif
    endEvent

**NOTES.**

The following are possible region IDs:

-   REGION\_UNKNOWN = -1

-   REGION\_PINEFOREST = 1

-   REGION\_VOLCANICTUNDRA = 2

-   REGION\_FALLFOREST = 3

-   REGION\_REACH = 4

-   REGION\_TUNDRA = 5

-   REGION\_TUNDRAMARSH = 6

-   REGION\_COAST = 7

-   REGION\_SNOW = 8

-   REGION\_OBLIVION = 9

-   REGION\_FALMERVALLEY = 10

-   REGION\_SOLSTHEIM = 11

-   REGION\_WYRMSTOOTH = 20

-   REGION\_DARKEND = 21

### FrostUtil/Frost\_OnInnerFireMeditate

**DESCRIPTION.**

An SKSE Mod Event that is raised when the player begins or ends using the Inner Fire ability.

**API VERSION ADDED.**

1

**SYNTAX.**

    Event Frost_OnInnerFireMeditate(bool abMeditating)

**PARAMETERS.**

-   abMeditating: If True, the player entered the Inner Fire state. If False, they have just left it.

**EXAMPLES.**

    Event OnInit()
        RegisterForModEvent("Frost_OnInnerFireMeditate", "Frost_OnInnerFireMeditate")
    endEvent

    Event Frost_OnInnerFireMeditate(bool abMeditating)
        if abMeditating
            debug.notification("The player began meditating using Inner Fire!")
        endif
    endEvent
