scriptname _Camp_ExaminePowerController extends ObjectReference
{Attached to object generated by power explosion.}

import CommonArrayHelper
import CampUtil
import _CampInternal

ObjectReference ReplacedRef
ObjectReference NewRef
ObjectReference[] ExtraDisabledRefs


Event OnInit()
	_Camp_Compatibility c = GetCompatibilitySystem()
	int i = 0
	int len = ArrayCountForm(c.ExamineFormsToReplace)
	bool replaced = false
	while i < len
		replaced = SearchAndReplaceForm(c, i)
		if replaced
			return
		endif
		i += 1
	endWhile

	if !replaced
		debug.notification("You don't see anything worth investigating.")
		;debug.notification("Nothing in particular stands out.")
		;debug.notification("You see nothing noteworthy.")
		;debug.notification("You don't see anything useful.")
		self.Disable()
		self.Delete()
	endif
EndEvent

bool function SearchAndReplaceForm(_Camp_Compatibility akCompatibility, int iIndex)
	ObjectReference ref = Game.FindClosestReferenceOfTypeFromRef(akCompatibility.ExamineFormsToReplace[iIndex], self, 512.0)
	if ref
		akCompatibility.ExamineSuccessMessages[iIndex].Show()
		ReplacedRef = ref
		NewRef = PlaceAndWaitFor3DLoaded(ReplacedRef, akCompatibility.ExamineReplacementForms[iIndex])
		NewRef.SetScale(ReplacedRef.GetScale())
		ReplacedRef.DisableNoWait()
		; shader effect on new thing
		if akCompatibility.ExamineExtraFormsToDisable[iIndex]
			DisableExtraObjects(akCompatibility.ExamineExtraFormsToDisable[iIndex])
		endif
		RegisterForSingleUpdateGameTime(72.0)
		return true
	else
		return false
	endif
endFunction

function DisableExtraObjects(FormList akExtraFormsToDisable)
	ExtraDisabledRefs = new ObjectReference[32]
	int list_size = akExtraFormsToDisable.GetSize()
	int i = 0
	while i < list_size
		ObjectReference ref = Game.FindClosestReferenceOfTypeFromRef(akExtraFormsToDisable.GetAt(i), self, 768.0)
		if ref
			ArrayAddRef(ExtraDisabledRefs, ref)
			ref.Disable()
		endif
		i += 1
	endWhile
endFunction

function EnableExtraObjects()
	if ExtraDisabledRefs
		int len = ArrayCountRef(ExtraDisabledRefs)
		if len > 0
			int i = 0
			while i > len
				ExtraDisabledRefs[i].EnableNoWait()
				i += 1
			endWhile
		endif
	endif
endFunction

Event OnUpdateGameTime()
	if ReplacedRef
		ReplacedRef.Enable()
	endif
	EnableExtraObjects()
	ReplacedRef = None
	NewRef = None
	ExtraDisabledRefs = None
	self.Disable()
	self.Delete()
EndEvent